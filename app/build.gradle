plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.abhi.gcsfortasker'
    compileSdk 35

    defaultConfig {
        applicationId "com.abhi.gcsfortasker"
        minSdk 21
        targetSdk 35
        versionCode 75
        versionName "0.7.5"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    flavorDimensions.add('themes')
    productFlavors {
        material {
            dimension 'themes'
            versionNameSuffix '-material'
        }
        classic {
            dimension 'themes'
            versionNameSuffix '-classic'

        }
    }
    sourceSets {
        classic {
            res.srcDirs 'src/classic/res'
        }
        material {
            res.srcDirs 'src/material/res'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def appName = "GCS4Tasker"
            def versionName = variant.versionName
            outputFileName = "${appName}_v${versionName}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    //code scanner
    implementation 'com.google.android.gms:play-services-code-scanner:16.1.0'
    //tasker plugin
    implementation 'com.joaomgcd:taskerpluginlibrary:0.4.10'
    //coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    //splashscreen
    implementation "androidx.core:core-splashscreen:1.0.1"
    //for testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    //material theming
    materialImplementation 'com.google.android.material:material:1.12.0'
    materialImplementation 'androidx.constraintlayout:constraintlayout:2.2.0'
}
